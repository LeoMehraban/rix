exp first: desc "gets the first element of a list" fn [list] [nth list 0];
exp second: desc "gets the second element of a list" fn [list] [nth list 1];
exp rest: desc "gets all of the list apart from the first element" fn [list] [second decons list];
exp reduce*: desc "recursivally applies 'fun' to 'prev' and the first element of 'list', calling reduce* again with the result of that evaluation as 'prev', and the remainder of the list as 'list'" fn [prev list fun]
	 [
		if (> len list 0)
		[
			reduce* (fun prev first list) # the new prev is the result of calling the input function on the original prev and the first element of the list
			(rest list) # the new list is the rest of the list
			$fun # the $ syntax gets the value of something from the current namespace, but doesn't evaluate it afterwards. this allows for functions to be gotten from the namespace without being called
		]
		[prev] # return the final result
	 ];

exp reduce: desc "applies the binary operation in 'fun' to elements of 'list', first applying it to the first two elements, then applying the result of that to the next element, and so on" fn [list fun]
    [
	reduce* (first list) (rest list) $fun
    ];
    	 
exp revcons: desc "pushes the element 'elt' onto the end of 'lst', returning 'lst'" fn [elt lst] '[@lst ,elt];
exp cons: desc "pushes the element 'elt' onto the beginning of 'lst', returning 'lst'" fn [elt lst] '[,elt @lst];

exp char: desc "turns a one-element string into a character int" fn [chr] [first (str>lst chr)];
exp replace-char: desc "replaces all instances of 'char' with 'replacement'" fn [str chr replacement]
    [ 
	lst>str
	reduce
	(str>lst str)
	fn [cur-str cur-char] [revcons (if (= cur-char chr) replacement cur-char) cur-str]
    ];

# 2 [0 1 2 3 4 5 6] > 1 [1 2 3 4 5 6] > 0 [2 3 4 5 6] > -1 [3 4 5 6]
exp after: desc "gets all of the list after index n"  fn [n list]
    [
	if (>= n 0)
	[after (- n 1) (rest list)]
	[list]
    ];

exp curry1: desc "returns a new function identical to 'func', but with 'val' as the first parameter" fn [func val]
	[
		consfn (rest fparams $func) # gets a list of all but the first parameter
		'[,$func ,$val @(rest fparams $func)] # makes a new func body. if 'func' was fn [x y] [+ x y], and 'val' was 6 then this new body would be [fn [x y] [+ x y] 6 y]
	];

exp curry: desc "returns a new function identical to 'func', but with its first parameters filled in with the values in 'vals'" vari [func vals]
    [
	consfn (after (- len vals 1) (fparams $func))
	'[,$func @vals @(after (- len vals 1) (fparams $func))]
    ];

